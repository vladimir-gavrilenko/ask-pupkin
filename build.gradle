buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'org.akhikhl.gretty:gretty:+'
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'war'
apply plugin: 'org.akhikhl.gretty'

// https://intellij-support.jetbrains.com/hc/en-us/community/posts/115000186970-How-to-Debug-webb-app-started-with-gradle-gretty
// Enable debugging of gretty started webapp in IDEA
// For debuging a gradle task, IDEA creates a gradle init script (in /tmp/ijinit#.gradle), that sets JVM arguments
// to enable remote debugging.
// Unfortunately by default that only works for gradle tasks that implement JavaForkOptions - which gretty doesn't do.
// So parse the JVM args that IDEA uses from that init script and set them as JVM args to be used by gretty:
def debugArgs = []
if (gradle.startParameter.initScripts) {
    def line = file(gradle.startParameter.initScripts[0]).readLines().findAll({x -> x ==~ /.*-agentlib:jdwp=.*/ })[0]
    if (line) {
        debugArgs = ((line =~ /.*'(.*)'.*/)[0][1] as String).split(' ')
    }
}

gretty {
    jvmArgs = [*debugArgs]
    httpPort = 8080
    contextPath = '/'
    servletContainer = 'jetty9'
}


repositories {
    mavenCentral()
}

sourceCompatibility = javaVersion
targetCompatibility = javaVersion

dependencies {
    compile group: 'org.springframework', name: 'spring-webmvc', version: springVersion
    compile group: 'org.springframework', name: 'spring-orm', version: springVersion

    compile group: 'org.springframework.security', name: 'spring-security-core', version: springSecurityVersion
    compile group: 'org.springframework.security', name: 'spring-security-config', version: springSecurityVersion
    compile group: 'org.springframework.security', name: 'spring-security-web', version: springSecurityVersion

    compile group: 'org.thymeleaf.extras', name: 'thymeleaf-extras-springsecurity4', version: thymeleafExtrasVersion
    compile group: 'org.thymeleaf', name: 'thymeleaf-spring4', version: thymeleafVersion

    compile group: 'org.webjars', name: 'bootstrap', version: bootstrapVersion
    compile group: 'org.webjars', name: 'jquery', version: jqueryVersion
    compile group: 'org.thymeleaf', name: 'thymeleaf', version: thymeleafVersion
    compile group: 'org.hibernate', name: 'hibernate-core', version: hibernateCoreVersion
    compile group: 'org.hibernate', name: 'hibernate-validator', version: hibernateValidatorVersion
    compile group: 'org.postgresql', name: 'postgresql', version: psqlVersion

    compile group: 'commons-fileupload', name: 'commons-fileupload', version: commonsFileUploadVersion

    providedCompile group: 'javax.servlet', name: 'javax.servlet-api', version: javaxServletVersion

    testCompile group: 'junit', name: 'junit', version: junitVersion
    testCompile group: 'org.springframework', name: 'spring-test', version: springVersion
    testCompile group: 'org.mockito', name: 'mockito-all', version: mockitoVersion
    testCompile group: 'org.hsqldb', name: 'hsqldb', version: hsqldbVersion
}
